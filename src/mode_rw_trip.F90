!TRP_LIC Copyright 1994-2014 CNRS, Meteo-France and Universite Paul Sabatier
!TRP_LIC This is part of the CTRIP software governed by the CeCILL-C licence
!TRP_LIC version 1. See LICENSE, CeCILL-C_V1-en.txt and CeCILL-C_V1-fr.txt  
!TRP_LIC for details. version 1.
!######################
MODULE MODE_RW_TRIP
!######################
!
!!****  *MODE_RW_TRIP*
!!
!!    PURPOSE
!!    -------
!
!      The purpose of this routine is to store here all routines
!      used by TRIP for read/write variables.
!
!!
!!**  IMPLICIT ARGUMENTS
!!    ------------------
!!       NONE
!!
!!    REFERENCE
!!    ---------
!!
!!
!!    AUTHOR
!!    ------
!!      B. Decharme       * Meteo France *
!!
!!    MODIFICATIONS
!!    -------------
!!      Original    25/04/08
!!      S.Sénési    08/11/16 : interface to XIOS
!--------------------------------------------------------------------------------
!
!*       0.    DECLARATIONS
!              ------------
!
USE MODD_TRIP_PAR, ONLY : XUNDEF, LNCPRINT
!
USE YOMHOOK ,      ONLY : LHOOK,   DR_HOOK
USE PARKIND1,      ONLY : JPRB
!
USE NETCDF
!
USE MODE_TRIP_NETCDF
!
INTERFACE READ_TRIP
    MODULE PROCEDURE READ_TRIP_X
    MODULE PROCEDURE READ_TRIP_XY
    MODULE PROCEDURE READ_TRIP_XYZ
END INTERFACE
!
INTERFACE WRITE_TRIP
    MODULE PROCEDURE WRITE_TRIP_X
    MODULE PROCEDURE WRITE_TRIP_XY
    MODULE PROCEDURE WRITE_TRIP_XYZ
END INTERFACE
!
!-------------------------------------------------------------------------------
!
 CONTAINS
!
!-------------------------------------------------------------------------------
!
!##################################################
SUBROUTINE READ_TRIP_X(KLISTING,HFILE,HVNAME,PREAD)
!##################################################
!
!!    PURPOSE
!!    -------
!
!     Read a X variable in a netcdf file
!
IMPLICIT NONE
!
!*      declarations of arguments
!
 CHARACTER(LEN=*), INTENT(IN) :: HFILE
 CHARACTER(LEN=*), INTENT(IN) :: HVNAME
!
INTEGER, INTENT(IN) :: KLISTING
!
REAL, DIMENSION(:), INTENT(OUT) :: PREAD
!
!*      declarations of local variables
!
 CHARACTER(LEN=NF90_MAX_NAME) :: YFILE
 CHARACTER(LEN=NF90_MAX_NAME) :: YVNAME
!
LOGICAL, PARAMETER     :: LRW = .FALSE.
!
INTEGER :: INCID
REAL(KIND=JPRB) :: ZHOOK_HANDLE
!
!*      procedure
!
IF (LHOOK) CALL DR_HOOK('MODE_RW_TRIP:READ_TRIP_X',0,ZHOOK_HANDLE)
YFILE  = HFILE (1:LEN_TRIM(HFILE ))
YVNAME = HVNAME(1:LEN_TRIM(HVNAME))
!
 CALL NCOPEN(KLISTING,LRW,LNCPRINT,YFILE,INCID)
 CALL NCREAD(KLISTING,INCID,YVNAME,PREAD,LNCPRINT)
 CALL NCCLOSE(KLISTING,LNCPRINT,YFILE,INCID)
!
IF (LHOOK) CALL DR_HOOK('MODE_RW_TRIP:READ_TRIP_X',1,ZHOOK_HANDLE)
!
END SUBROUTINE READ_TRIP_X
!
!-------------------------------------------------------------------------------
!
!##################################################
SUBROUTINE READ_TRIP_XY(KLISTING,HFILE,HVNAME,PREAD)
!##################################################
!
!!    PURPOSE
!!    -------
!
!     Read a XY variable in a netcdf file
!
IMPLICIT NONE
!
!*      declarations of arguments
!
 CHARACTER(LEN=*), INTENT(IN) :: HFILE
 CHARACTER(LEN=*), INTENT(IN) :: HVNAME
!
INTEGER, INTENT(IN) :: KLISTING
!
REAL, DIMENSION(:,:), INTENT(OUT) :: PREAD
!
!*      declarations of local variables
!
 CHARACTER(LEN=NF90_MAX_NAME) :: YFILE
 CHARACTER(LEN=NF90_MAX_NAME) :: YVNAME
!
LOGICAL, PARAMETER     :: LRW = .FALSE.
!
INTEGER :: INCID
REAL(KIND=JPRB) :: ZHOOK_HANDLE
!
!*      procedure
!
IF (LHOOK) CALL DR_HOOK('MODE_RW_TRIP:READ_TRIP_XY',0,ZHOOK_HANDLE)
YFILE  = HFILE (1:LEN_TRIM(HFILE ))
YVNAME = HVNAME(1:LEN_TRIM(HVNAME))
!
 CALL NCOPEN(KLISTING,LRW,LNCPRINT,YFILE,INCID)
 CALL NCREAD(KLISTING,INCID,YVNAME,PREAD,LNCPRINT)
 CALL NCCLOSE(KLISTING,LNCPRINT,YFILE,INCID)
!
IF (LHOOK) CALL DR_HOOK('MODE_RW_TRIP:READ_TRIP_XY',1,ZHOOK_HANDLE)
!
END SUBROUTINE READ_TRIP_XY
!
!-------------------------------------------------------------------------------
!
!##################################################
SUBROUTINE READ_TRIP_XYZ(KLISTING,HFILE,HVNAME,PREAD)
!##################################################
!
!!    PURPOSE
!!    -------
!
!     Read a XYZ variable in a netcdf file
!
IMPLICIT NONE
!
!*      declarations of arguments
!
 CHARACTER(LEN=*), INTENT(IN) :: HFILE
 CHARACTER(LEN=*), INTENT(IN) :: HVNAME
!
INTEGER, INTENT(IN) :: KLISTING
!
REAL, DIMENSION(:,:,:), INTENT(OUT) :: PREAD
!
!*      declarations of local variables
!
 CHARACTER(LEN=NF90_MAX_NAME) :: YFILE
 CHARACTER(LEN=NF90_MAX_NAME) :: YVNAME
!
LOGICAL, PARAMETER       :: LRW = .FALSE.
!
INTEGER :: INCID
REAL(KIND=JPRB) :: ZHOOK_HANDLE
!
!*      procedure
!
IF (LHOOK) CALL DR_HOOK('MODE_RW_TRIP:READ_TRIP_XYZ',0,ZHOOK_HANDLE)
YFILE  = HFILE (1:LEN_TRIM(HFILE ))
YVNAME = HVNAME(1:LEN_TRIM(HVNAME))
!
 CALL NCOPEN(KLISTING,LRW,LNCPRINT,YFILE,INCID)
 CALL NCREAD(KLISTING,INCID,YVNAME,PREAD,LNCPRINT)
 CALL NCCLOSE(KLISTING,LNCPRINT,YFILE,INCID)
!
IF (LHOOK) CALL DR_HOOK('MODE_RW_TRIP:READ_TRIP_XYZ',1,ZHOOK_HANDLE)
!
END SUBROUTINE READ_TRIP_XYZ
!
!-------------------------------------------------------------------------------
!
!######################################################################
SUBROUTINE WRITE_TRIP_X(KLISTING,HFILE,HVNAME,OMASK,PWRITE,KTNUM,KTVAL,ODOUBLE,OXIOS)
!######################################################################
!
!!    PURPOSE
!!    -------
!
!     Write a X variable in HFILE
!
#ifdef WXIOS
USE XIOS
#endif
!
IMPLICIT NONE
!
!*      declarations of arguments
!
CHARACTER(LEN=*), INTENT(IN) :: HFILE
CHARACTER(LEN=*), INTENT(IN) :: HVNAME
!
INTEGER, INTENT(IN) :: KLISTING
!
LOGICAL, DIMENSION(:), INTENT(IN) :: OMASK
REAL,    DIMENSION(:), INTENT(IN) :: PWRITE
!
INTEGER, INTENT(IN), OPTIONAL :: KTNUM
INTEGER, INTENT(IN), OPTIONAL :: KTVAL
LOGICAL, INTENT(IN), OPTIONAL :: ODOUBLE
LOGICAL, INTENT(IN), OPTIONAL :: OXIOS
!
!*      declarations of local variables
!
CHARACTER(LEN=NF90_MAX_NAME) :: YFILE
CHARACTER(LEN=NF90_MAX_NAME) :: YVNAME
!
LOGICAL, PARAMETER     :: LRW = .TRUE.
!
REAL, DIMENSION(SIZE(PWRITE),1) :: ZWRITE
!
INTEGER :: INCID
LOGICAL :: GXIOS, LLAKE
REAL(KIND=JPRB) :: ZHOOK_HANDLE
!
!*      procedure
!
IF (LHOOK) CALL DR_HOOK('MODE_RW_TRIP:WRITE_TRIP_X',0,ZHOOK_HANDLE)
!
YFILE  = HFILE (1:LEN_TRIM(HFILE ))
YVNAME = HVNAME(1:LEN_TRIM(HVNAME))
GXIOS=.FALSE.
!
WHERE(OMASK)
  ZWRITE(:,1) = PWRITE(:)
ELSEWHERE
  ZWRITE(:,1) = XUNDEF
ENDWHERE
!
IF (PRESENT(OXIOS)) GXIOS=OXIOS
!
IF (GXIOS) THEN
#ifdef WXIOS
   CALL XIOS_SEND_FIELD(YVNAME,ZWRITE)
#endif
ELSE
  CALL NCOPEN(KLISTING,LRW,LNCPRINT,YFILE,INCID)
  IF(PRESENT(KTNUM).AND.PRESENT(KTVAL).AND.PRESENT(ODOUBLE))THEN
    CALL NCSTORE(KLISTING,INCID,YVNAME,ZWRITE,LNCPRINT,KTNUM,KTVAL,ODOUBLE=ODOUBLE,OWRITE1D=.TRUE.)
  ELSEIF(PRESENT(KTNUM).AND.PRESENT(KTVAL))THEN
    CALL NCSTORE(KLISTING,INCID,YVNAME,ZWRITE,LNCPRINT,KTNUM,KTVAL,OWRITE1D=.TRUE.)
  ELSEIF(PRESENT(ODOUBLE))THEN
    CALL NCSTORE(KLISTING,INCID,YVNAME,ZWRITE,LNCPRINT,ODOUBLE=ODOUBLE,OWRITE1D=.TRUE.)
  ELSE
    CALL NCSTORE(KLISTING,INCID,YVNAME,ZWRITE,LNCPRINT,OWRITE1D=.TRUE.)
  ENDIF
  CALL NCCLOSE(KLISTING,LNCPRINT,YFILE,INCID)
ENDIF
!
IF (LHOOK) CALL DR_HOOK('MODE_RW_TRIP:WRITE_TRIP_X',1,ZHOOK_HANDLE)
!
END SUBROUTINE WRITE_TRIP_X
!
!-------------------------------------------------------------------------------
!
!######################################################################
SUBROUTINE WRITE_TRIP_XY(KLISTING,HFILE,HVNAME,OMASK,PWRITE,KTNUM,KTVAL,ODOUBLE,OXIOS)
!######################################################################
!
!!    PURPOSE
!!    -------
!
!     Write a XY variable in HFILE
!
#ifdef WXIOS
USE XIOS
#endif
!
IMPLICIT NONE
!
!*      declarations of arguments
!
 CHARACTER(LEN=*), INTENT(IN) :: HFILE
 CHARACTER(LEN=*), INTENT(IN) :: HVNAME
!
INTEGER, INTENT(IN) :: KLISTING
!
LOGICAL, DIMENSION(:,:), INTENT(IN) :: OMASK
REAL,    DIMENSION(:,:), INTENT(IN) :: PWRITE
!
INTEGER, INTENT(IN), OPTIONAL :: KTNUM
INTEGER, INTENT(IN), OPTIONAL :: KTVAL
LOGICAL, INTENT(IN), OPTIONAL :: ODOUBLE
LOGICAL, INTENT(IN), OPTIONAL :: OXIOS
!
!*      declarations of local variables
!
 CHARACTER(LEN=NF90_MAX_NAME) :: YFILE
 CHARACTER(LEN=NF90_MAX_NAME) :: YVNAME
!
LOGICAL, PARAMETER     :: LRW = .TRUE.
!
REAL, DIMENSION(SIZE(PWRITE,1),SIZE(PWRITE,2)) :: ZWRITE
!
INTEGER :: INCID
LOGICAL :: GXIOS
REAL(KIND=JPRB) :: ZHOOK_HANDLE
!
!*      procedure
!
IF (LHOOK) CALL DR_HOOK('MODE_RW_TRIP:WRITE_TRIP_XY',0,ZHOOK_HANDLE)
YFILE  = HFILE (1:LEN_TRIM(HFILE ))
YVNAME = HVNAME(1:LEN_TRIM(HVNAME))
GXIOS=.FALSE.
!
WHERE(OMASK)
    ZWRITE(:,:) = PWRITE(:,:)
ELSEWHERE
    ZWRITE(:,:) = XUNDEF
ENDWHERE
!
IF (PRESENT(OXIOS)) GXIOS=OXIOS
!
IF (GXIOS) THEN
#ifdef WXIOS
   CALL XIOS_SEND_FIELD(YVNAME,ZWRITE)
#endif
ELSE
  CALL NCOPEN(KLISTING,LRW,LNCPRINT,YFILE,INCID)
  IF(PRESENT(KTNUM).AND.PRESENT(KTVAL).AND.PRESENT(ODOUBLE))THEN
    CALL NCSTORE(KLISTING,INCID,YVNAME,ZWRITE,LNCPRINT,KTNUM,KTVAL,ODOUBLE=ODOUBLE)
  ELSEIF(PRESENT(KTNUM).AND.PRESENT(KTVAL))THEN
    CALL NCSTORE(KLISTING,INCID,YVNAME,ZWRITE,LNCPRINT,KTNUM,KTVAL)
  ELSEIF(PRESENT(ODOUBLE))THEN
    CALL NCSTORE(KLISTING,INCID,YVNAME,ZWRITE,LNCPRINT,ODOUBLE=ODOUBLE)
  ELSE
    CALL NCSTORE(KLISTING,INCID,YVNAME,ZWRITE,LNCPRINT)
  ENDIF
  CALL NCCLOSE(KLISTING,LNCPRINT,YFILE,INCID)
ENDIF
!
IF (LHOOK) CALL DR_HOOK('MODE_RW_TRIP:WRITE_TRIP_XY',1,ZHOOK_HANDLE)
!
END SUBROUTINE WRITE_TRIP_XY
!
!-------------------------------------------------------------------------------
!
!#######################################################################
SUBROUTINE WRITE_TRIP_XYZ(KLISTING,HFILE,HVNAME,OMASK,PWRITE,KTNUM,KTVAL,ODOUBLE,OXIOS)
!#######################################################################
!
!!    PURPOSE
!!    -------
!
!     Write a XY variable in HFILE
!
#ifdef WXIOS
USE XIOS
#endif
!
IMPLICIT NONE
!
!*      declarations of arguments
!
 CHARACTER(LEN=*), INTENT(IN) :: HFILE
 CHARACTER(LEN=*), INTENT(IN) :: HVNAME
!
INTEGER, INTENT(IN) :: KLISTING
!
LOGICAL, DIMENSION(:,:),   INTENT(IN) :: OMASK
REAL,    DIMENSION(:,:,:), INTENT(IN) :: PWRITE
!
INTEGER, INTENT(IN), OPTIONAL :: KTNUM
INTEGER, INTENT(IN), OPTIONAL :: KTVAL
LOGICAL, INTENT(IN), OPTIONAL :: ODOUBLE
LOGICAL, INTENT(IN), OPTIONAL :: OXIOS
!
!*      declarations of local variables
!
 CHARACTER(LEN=NF90_MAX_NAME) :: YFILE
 CHARACTER(LEN=NF90_MAX_NAME) :: YVNAME
!
LOGICAL, PARAMETER     :: LRW = .TRUE.
LOGICAL, PARAMETER     :: LZW = .TRUE.
!
REAL, DIMENSION(SIZE(PWRITE,1),SIZE(PWRITE,2),SIZE(PWRITE,3)) :: ZWRITE
!
LOGICAL :: GXIOS
INTEGER :: INCID, IZLEN, JZ
REAL(KIND=JPRB) :: ZHOOK_HANDLE
!
!*      procedure
!
IF (LHOOK) CALL DR_HOOK('MODE_RW_TRIP:WRITE_TRIP_XYZ',0,ZHOOK_HANDLE)
IZLEN=SIZE(PWRITE,3)
GXIOS=.FALSE.
!
YFILE  = HFILE (1:LEN_TRIM(HFILE ))
YVNAME = HVNAME(1:LEN_TRIM(HVNAME))
!
DO JZ=1,IZLEN
   WHERE(OMASK(:,:))
         ZWRITE(:,:,JZ) = PWRITE(:,:,JZ)
   ELSEWHERE
         ZWRITE(:,:,JZ)=XUNDEF
   ENDWHERE
ENDDO
!
IF (PRESENT(OXIOS)) GXIOS=OXIOS
!
IF (GXIOS) THEN
#ifdef WXIOS
   CALL XIOS_SEND_FIELD(YVNAME,ZWRITE(:,:,:))
#endif
ELSE
   CALL NCOPEN(KLISTING,LRW,LNCPRINT,YFILE,INCID)
   DO JZ=1,IZLEN
      IF(PRESENT(KTNUM).AND.PRESENT(KTVAL).AND.PRESENT(ODOUBLE))THEN
         CALL NCSTORE(KLISTING,INCID,YVNAME,ZWRITE(:,:,JZ),LNCPRINT,KTNUM,KTVAL,JZ,LZW,ODOUBLE=ODOUBLE)
      ELSEIF(PRESENT(KTNUM).AND.PRESENT(KTVAL))THEN
         CALL NCSTORE(KLISTING,INCID,YVNAME,ZWRITE(:,:,JZ),LNCPRINT,KTNUM,KTVAL,JZ,LZW)
      ELSEIF(PRESENT(ODOUBLE))THEN
         CALL NCSTORE(KLISTING,INCID,YVNAME,ZWRITE(:,:,JZ),LNCPRINT,KLEVEL=JZ,OVARZDIM=LZW,ODOUBLE=ODOUBLE)
      ELSE
         CALL NCSTORE(KLISTING,INCID,YVNAME,ZWRITE(:,:,JZ),LNCPRINT,KLEVEL=JZ,OVARZDIM=LZW)
      ENDIF
   ENDDO
   CALL NCCLOSE(KLISTING,LNCPRINT,YFILE,INCID)
ENDIF
!
IF (LHOOK) CALL DR_HOOK('MODE_RW_TRIP:WRITE_TRIP_XYZ',1,ZHOOK_HANDLE)
!
END SUBROUTINE WRITE_TRIP_XYZ
!
!-------------------------------------------------------------------------------
!
!######################################################################
SUBROUTINE WRITE_TRIP_DATE(KLISTING,HFILE,KYEAR,KMONTH,KDAY,PTIME)
!######################################################################
!
!!    PURPOSE
!!    -------
!
!     Write date in HFILE
!
IMPLICIT NONE
!
!*      declarations of arguments
!
 CHARACTER(LEN=*), INTENT(IN) :: HFILE
!
INTEGER, INTENT(IN)          :: KLISTING
INTEGER, INTENT(IN)          :: KYEAR
INTEGER, INTENT(IN)          :: KMONTH
INTEGER, INTENT(IN)          :: KDAY
REAL,    INTENT(IN)          :: PTIME
!
!*      declarations of local variables
!
 CHARACTER(LEN=NF90_MAX_NAME) :: YFILE
!
LOGICAL, PARAMETER     :: LRW = .TRUE.
!
INTEGER :: INCID
!
REAL(KIND=JPRB) :: ZHOOK_HANDLE
!
!*      procedure
!
IF (LHOOK) CALL DR_HOOK('MODE_RW_TRIP:WRITE_TRIP_DATE',0,ZHOOK_HANDLE)
!
YFILE  = HFILE (1:LEN_TRIM(HFILE ))
!
 CALL NCOPEN(KLISTING,LRW,LNCPRINT,YFILE,INCID)
 CALL NCDATE(KLISTING,INCID,KYEAR,KMONTH,KDAY,PTIME,LNCPRINT)
 CALL NCCLOSE(KLISTING,LNCPRINT,YFILE,INCID)
!
IF (LHOOK) CALL DR_HOOK('MODE_RW_TRIP:WRITE_TRIP_DATE',1,ZHOOK_HANDLE)
!
END SUBROUTINE WRITE_TRIP_DATE
!
!-------------------------------------------------------------------------------
!
END MODULE MODE_RW_TRIP
