!TRP_LIC Copyright 1994-2014 CNRS, Meteo-France and Universite Paul Sabatier
!TRP_LIC This is part of the CTRIP software governed by the CeCILL-C licence
!TRP_LIC version 1. See LICENSE, CeCILL-C_V1-en.txt and CeCILL-C_V1-fr.txt  
!TRP_LIC for details. version 1.
!######################
MODULE MODE_TRIP_NETCDF
!######################
!
!!****  *MODE_TRIP_NETCDF*
!!
!!    PURPOSE
!!    -------
!
!      The purpose of this routine is to store here all routines
!      used by TRIP for read/store variables in netcdf.
!
!!
!!**  IMPLICIT ARGUMENTS
!!    ------------------
!!       NONE
!!
!!    REFERENCE
!!    ---------
!!
!!
!!    AUTHOR
!!    ------
!!      B. Decharme       * Meteo France *
!!      S. Munier   03/2020    CTRIP-12D and parallelization
!!      T. Guinaldo 04/2020    Add MLake
!!
!!    MODIFICATIONS
!!    -------------
!!      Original    25/04/08
!!      S. Munier   28/03/2020 CTRIP-12D and parallelization
!--------------------------------------------------------------------------------
!
!*       0.    DECLARATIONS
!              ------------
!
USE MODD_TRIP_PAR,ONLY : XUNDEF
!
USE MODI_ABORT_TRIP
!
USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK
USE PARKIND1  ,ONLY : JPRB
!
USE NETCDF
!
INTERFACE NCREAD
    MODULE PROCEDURE NCREAD_X
    MODULE PROCEDURE NCREAD_XY
    MODULE PROCEDURE NCREAD_XYZ
END INTERFACE
!
!-------------------------------------------------------------------------------
!
CONTAINS
!
!-------------------------------------------------------------------------------
!
!######################################################
SUBROUTINE NCOPEN(KLISTING,ORW,OVERBOSE,HFILENAME,KNCID)
!######################################################
!
!!    PURPOSE
!!    -------
!
!     Open a netcdf file name YFILENAME
!
IMPLICIT NONE
!
!*      declarations of arguments
!
CHARACTER(LEN=NF90_MAX_NAME), INTENT(IN) :: HFILENAME
!
LOGICAL, INTENT(IN)          :: ORW, OVERBOSE
INTEGER, INTENT(IN)          :: KLISTING
INTEGER, INTENT(OUT)         :: KNCID
!
!*      declarations of local variables
!
CHARACTER(LEN=NF90_MAX_NAME) :: YFNAME
!
INTEGER :: IC
REAL(KIND=JPRB) :: ZHOOK_HANDLE
!
!*      procedure
!
IF (LHOOK) CALL DR_HOOK('MODE_TRIP_NETCDF:NCOPEN',0,ZHOOK_HANDLE)
!
YFNAME = HFILENAME(1:LEN_TRIM(HFILENAME))
!
IF(ORW)THEN
  IC = NF90_OPEN(YFNAME,NF90_WRITE,KNCID)
ELSE
  IC = NF90_OPEN(YFNAME,NF90_NOWRITE,KNCID)
ENDIF
!
IF(IC/=NF90_NOERR)THEN
  WRITE(KLISTING,*)'NCOPEN for TRIP : Error opening file ',HFILENAME(1:LEN_TRIM(HFILENAME))
  WRITE(KLISTING,*)NF90_STRERROR(IC)
  CALL ABORT_TRIP('NCOPEN for TRIP : Error opening file')
ELSEIF(OVERBOSE)THEN
  WRITE(KLISTING,*)'NCOPEN for TRIP : Opening file ',HFILENAME(1:LEN_TRIM(HFILENAME))
ENDIF
!
IF (LHOOK) CALL DR_HOOK('MODE_TRIP_NETCDF:NCOPEN',1,ZHOOK_HANDLE)
!
END SUBROUTINE NCOPEN
!
!-------------------------------------------------------------------------------
!
!###################################################
SUBROUTINE NCCLOSE(KLISTING,OVERBOSE,HFILENAME,KNCID)
!###################################################
!
!!    PURPOSE
!!    -------
!
!     Close a netcdf file
!
IMPLICIT NONE
!
!*      declarations of arguments
!
CHARACTER(LEN=NF90_MAX_NAME), INTENT(IN) :: HFILENAME
!
LOGICAL, INTENT(IN)          :: OVERBOSE
INTEGER, INTENT(IN)          :: KLISTING
INTEGER, INTENT(IN)          :: KNCID
!
!*      declarations of local variables
!
INTEGER :: IC
REAL(KIND=JPRB) :: ZHOOK_HANDLE
!
!*      procedure
!
IF (LHOOK) CALL DR_HOOK('MODE_TRIP_NETCDF:NCCLOSE',0,ZHOOK_HANDLE)
!
IC = NF90_CLOSE(KNCID)
IF(OVERBOSE)WRITE(KLISTING,*)'NCCLOSE for TRIP : Close file ',HFILENAME(1:LEN_TRIM(HFILENAME))
!
IF (LHOOK) CALL DR_HOOK('MODE_TRIP_NETCDF:NCCLOSE',1,ZHOOK_HANDLE)
!
END SUBROUTINE NCCLOSE
!
!-------------------------------------------------------------------------------
!
!#########################################################################
SUBROUTINE NCREAD_X(KLISTING,KNCID,HVNAME,PVECT,OVERBOSE)
!#########################################################################
!
!!    PURPOSE
!!    -------
!
!     Read a XY variable in a netcdf file
!
IMPLICIT NONE
!
!*      declarations of arguments
!
CHARACTER(LEN=NF90_MAX_NAME), INTENT(IN) :: HVNAME
!
INTEGER, INTENT(IN)          :: KLISTING
INTEGER, INTENT(IN)          :: KNCID
!
LOGICAL, INTENT(IN)          :: OVERBOSE
!
REAL, DIMENSION(:), INTENT(OUT) :: PVECT
!
!*      declarations of local variables
!
CHARACTER(LEN=NF90_MAX_NAME) :: YVNAME
!
REAL*4,  DIMENSION(SIZE(PVECT)) :: ZVECT
INTEGER, DIMENSION(SIZE(PVECT)) :: IVECT
!
REAL    :: ZMISS
REAL*4  :: ZMISS4
!
INTEGER :: IC, ID, IVAR_TYPE
!
REAL(KIND=JPRB) :: ZHOOK_HANDLE
!
!*      procedure
!
IF (LHOOK) CALL DR_HOOK('MODE_TRIP_NETCDF:NCREAD_X',0,ZHOOK_HANDLE)
!
YVNAME = HVNAME(1:LEN_TRIM(HVNAME))
!
IC = NF90_INQ_VARID(KNCID,YVNAME,ID)
IF(IC/=NF90_NOERR)THEN
  WRITE(KLISTING,*)'NCREAD_X for TRIP : Error reading id for variable ',HVNAME(1:LEN_TRIM(HVNAME))
  WRITE(KLISTING,*)NF90_STRERROR(IC)
  CALL ABORT_TRIP('NCREAD_X for TRIP : Error reading id variable ')
ENDIF
!
IC = NF90_INQUIRE_VARIABLE(KNCID,ID,XTYPE=IVAR_TYPE)
IF(IC/=NF90_NOERR)THEN
  WRITE(KLISTING,*)'NCREAD_X for TRIP : Error reading type for variable ',HVNAME(1:LEN_TRIM(HVNAME))
  WRITE(KLISTING,*)NF90_STRERROR(IC)
  CALL ABORT_TRIP('NCREAD_X for TRIP : Error reading type variable ')
ENDIF
!
IF(IVAR_TYPE==NF90_DOUBLE)THEN
  IC=NF90_GET_ATT(KNCID,ID,'missing_value',ZMISS)
  IF(IC/=NF90_NOERR) IC=NF90_GET_ATT(KNCID,ID,'_FillValue',ZMISS)
  IF(IC/=NF90_NOERR) ZMISS = 1e20
  IC=NF90_GET_VAR(KNCID,ID,PVECT)
  WHERE(PVECT(:)==ZMISS)
        PVECT(:)=XUNDEF
  ENDWHERE
ELSEIF(IVAR_TYPE==NF90_FLOAT)THEN
  IC=NF90_GET_ATT(KNCID,ID,'missing_value',ZMISS4)
  IF(IC/=NF90_NOERR) IC=NF90_GET_ATT(KNCID,ID,'_FillValue',ZMISS4)
  IF(IC/=NF90_NOERR) ZMISS4 = 1e20
  IC=NF90_GET_VAR(KNCID,ID,ZVECT)
  WHERE(ZVECT(:)/=ZMISS4)
        PVECT(:)=ZVECT(:)
  ELSEWHERE
        PVECT(:)=XUNDEF
  ENDWHERE
ELSEIF(IVAR_TYPE==NF90_INT)THEN
  IC=NF90_GET_VAR(KNCID,ID,IVECT)
  PVECT(:)=REAL(IVECT(:))
ELSE
  WRITE(KLISTING,*)'NCREAD_X for TRIP : type of the variable must be integer, float or double ',HVNAME(1:LEN_TRIM(HVNAME))
  CALL ABORT_TRIP('NCREAD_X for TRIP : type of the variable not good')
ENDIF
IF(IC/=NF90_NOERR)THEN
  WRITE(KLISTING,*)'NCREAD_X for TRIP : Error reading variable ',HVNAME(1:LEN_TRIM(HVNAME))
  WRITE(KLISTING,*)NF90_STRERROR(IC)
  CALL ABORT_TRIP('NCREAD_X for TRIP : Error reading variable')
ELSEIF(OVERBOSE)THEN
  WRITE(KLISTING,*)'NCREAD_X for TRIP : Success in reading variable ',HVNAME(1:LEN_TRIM(HVNAME))
ENDIF
!
IF (LHOOK) CALL DR_HOOK('MODE_TRIP_NETCDF:NCREAD_X',1,ZHOOK_HANDLE)
!
END SUBROUTINE NCREAD_X
!-------------------------------------------------------------------------------
!
!#########################################################################
SUBROUTINE NCREAD_XY(KLISTING,KNCID,HVNAME,PVECT,OVERBOSE)
!#########################################################################
!
!!    PURPOSE
!!    -------
!
!     Read a XY variable in a netcdf file
!
IMPLICIT NONE
!
!*      declarations of arguments
!
CHARACTER(LEN=NF90_MAX_NAME), INTENT(IN) :: HVNAME
!
INTEGER, INTENT(IN)          :: KLISTING
INTEGER, INTENT(IN)          :: KNCID
!
LOGICAL, INTENT(IN)          :: OVERBOSE
!
REAL, DIMENSION(:,:), INTENT(OUT) :: PVECT
!
!*      declarations of local variables
!
CHARACTER(LEN=NF90_MAX_NAME) :: YVNAME
!
REAL*4, DIMENSION(SIZE(PVECT,1),SIZE(PVECT,2)) :: ZVECT
!
REAL    :: ZMISS
REAL*4  :: ZMISS4
!
INTEGER :: IC, ID, IVAR_TYPE
!
REAL(KIND=JPRB) :: ZHOOK_HANDLE
!
!*      procedure
!
IF (LHOOK) CALL DR_HOOK('MODE_TRIP_NETCDF:NCREAD_XY',0,ZHOOK_HANDLE)
!
YVNAME = HVNAME(1:LEN_TRIM(HVNAME))
!
IC = NF90_INQ_VARID(KNCID,YVNAME,ID)
IF(IC/=NF90_NOERR)THEN
  WRITE(KLISTING,*)'NCREAD_XY for TRIP : Error reading id for variable ',HVNAME(1:LEN_TRIM(HVNAME))
  WRITE(KLISTING,*)NF90_STRERROR(IC)
  CALL ABORT_TRIP('NCREAD_XY for TRIP : Error reading id variable ')
ENDIF
!
IC = NF90_INQUIRE_VARIABLE(KNCID,ID,XTYPE=IVAR_TYPE)
IF(IC/=NF90_NOERR)THEN
  WRITE(KLISTING,*)'NCREAD_XY for TRIP : Error reading type for variable ',HVNAME(1:LEN_TRIM(HVNAME))
  WRITE(KLISTING,*)NF90_STRERROR(IC)
  CALL ABORT_TRIP('NCREAD_XY for TRIP : Error reading type variable ')
ENDIF
!
IF(IVAR_TYPE==NF90_DOUBLE)THEN
  IC=NF90_GET_ATT(KNCID,ID,'missing_value',ZMISS)
  IF(IC/=NF90_NOERR) IC=NF90_GET_ATT(KNCID,ID,'_FillValue',ZMISS)
  IF(IC/=NF90_NOERR) ZMISS = 1e20
  IC=NF90_GET_VAR(KNCID,ID,PVECT)
  WHERE(PVECT(:,:)==ZMISS)
        PVECT(:,:)=XUNDEF
  ENDWHERE
ELSEIF(IVAR_TYPE==NF90_FLOAT)THEN
  IC=NF90_GET_ATT(KNCID,ID,'missing_value',ZMISS4)
  IF(IC/=NF90_NOERR) IC=NF90_GET_ATT(KNCID,ID,'_FillValue',ZMISS4)
  IF(IC/=NF90_NOERR) ZMISS4 = 1e20
  IC=NF90_GET_VAR(KNCID,ID,ZVECT)
  WHERE(ZVECT(:,:)/=ZMISS4)
        PVECT(:,:)=ZVECT(:,:)
  ELSEWHERE
        PVECT(:,:)=XUNDEF
  ENDWHERE
ELSE
  WRITE(KLISTING,*)'NCREAD_XY for TRIP : type of the variable must be float or double ',HVNAME(1:LEN_TRIM(HVNAME))
  CALL ABORT_TRIP('NCREAD_XY for TRIP : type of the variable not good')
ENDIF
!
IF(IC/=NF90_NOERR)THEN
  WRITE(KLISTING,*)'NCREAD_XY for TRIP : Error reading variable ',HVNAME(1:LEN_TRIM(HVNAME))
  WRITE(KLISTING,*)NF90_STRERROR(IC)
  CALL ABORT_TRIP('NCREAD_XY for TRIP : Error reading variable')
ELSEIF(OVERBOSE)THEN
  WRITE(KLISTING,*)'NCREAD_XY for TRIP : Success in reading variable ',HVNAME(1:LEN_TRIM(HVNAME))
ENDIF
!
IF (LHOOK) CALL DR_HOOK('MODE_TRIP_NETCDF:NCREAD_XY',1,ZHOOK_HANDLE)
!
END SUBROUTINE NCREAD_XY
!-------------------------------------------------------------------------------
!
!#########################################################################
SUBROUTINE NCREAD_XYZ(KLISTING,KNCID,HVNAME,PVECT,OVERBOSE)
!#########################################################################
!
!!    PURPOSE
!!    -------
!
!     Read a XYZ variable in a netcdf file
!
IMPLICIT NONE
!
!*      declarations of arguments
!
CHARACTER(LEN=NF90_MAX_NAME), INTENT(IN) :: HVNAME
!
INTEGER, INTENT(IN)          :: KLISTING
INTEGER, INTENT(IN)          :: KNCID
!
LOGICAL, INTENT(IN)          :: OVERBOSE
!
REAL, DIMENSION(:,:,:), INTENT(OUT) :: PVECT
!
!*      declarations of local variables
!
CHARACTER(LEN=NF90_MAX_NAME) :: YVNAME
!
REAL*4, DIMENSION(SIZE(PVECT,1),SIZE(PVECT,2),SIZE(PVECT,3)) :: ZVECT
!
REAL    :: ZMISS
REAL*4  :: ZMISS4
!
INTEGER :: IC, ID, IVAR_TYPE
!
REAL(KIND=JPRB) :: ZHOOK_HANDLE
!
!*      procedure
!
IF (LHOOK) CALL DR_HOOK('MODE_TRIP_NETCDF:NCREAD_XYZ',0,ZHOOK_HANDLE)
YVNAME = HVNAME(1:LEN_TRIM(HVNAME))
!
IC = NF90_INQ_VARID(KNCID,YVNAME,ID)
!
IC = NF90_INQ_VARID(KNCID,YVNAME,ID)
IF(IC/=NF90_NOERR)THEN
  WRITE(KLISTING,*)'NCREAD_XYZ for TRIP : Error reading id for variable ',HVNAME(1:LEN_TRIM(HVNAME))
  WRITE(KLISTING,*)NF90_STRERROR(IC)
  CALL ABORT_TRIP('NCREAD_XYZ for TRIP : Error reading id variable ')
ENDIF
!
IC = NF90_INQUIRE_VARIABLE(KNCID,ID,XTYPE=IVAR_TYPE)
IF(IC/=NF90_NOERR)THEN
  WRITE(KLISTING,*)'NCREAD_XYZ for TRIP : Error reading type for variable ',HVNAME(1:LEN_TRIM(HVNAME))
  WRITE(KLISTING,*)NF90_STRERROR(IC)
  CALL ABORT_TRIP('NCREAD_XYZ for TRIP : Error reading type variable ')
ENDIF
!
IF(IVAR_TYPE==NF90_DOUBLE)THEN
  IC=NF90_GET_ATT(KNCID,ID,'missing_value',ZMISS)
  IF(IC/=NF90_NOERR) IC=NF90_GET_ATT(KNCID,ID,'_FillValue',ZMISS)
  IF(IC/=NF90_NOERR) ZMISS = 1e20
  IC=NF90_GET_VAR(KNCID,ID,PVECT)
  WHERE(PVECT(:,:,:)==ZMISS)
        PVECT(:,:,:)=XUNDEF
  ENDWHERE
ELSEIF(IVAR_TYPE==NF90_FLOAT)THEN
  IC=NF90_GET_ATT(KNCID,ID,'missing_value',ZMISS4)
  IF(IC/=NF90_NOERR) IC=NF90_GET_ATT(KNCID,ID,'_FillValue',ZMISS4)
  IF(IC/=NF90_NOERR) ZMISS4 = 1e20
  IC=NF90_GET_VAR(KNCID,ID,ZVECT)
  WHERE(ZVECT(:,:,:)/=ZMISS4)
        PVECT(:,:,:)=ZVECT(:,:,:)
  ELSEWHERE
        PVECT(:,:,:)=XUNDEF
  ENDWHERE
ELSE
  WRITE(KLISTING,*)'NCREAD_XYZ for TRIP : type of the variable must be float or double ',HVNAME(1:LEN_TRIM(HVNAME))
  CALL ABORT_TRIP('NCREAD_XYZ for TRIP : type of the variable not good')
ENDIF
IF(IC/=NF90_NOERR)THEN
  WRITE(KLISTING,*)'NCREAD_XYZ for TRIP : Error reading variable ',HVNAME(1:LEN_TRIM(HVNAME))
  WRITE(KLISTING,*)NF90_STRERROR(IC)
  CALL ABORT_TRIP('NCREAD_XYZ for TRIP : Error reading variable')
ELSEIF(OVERBOSE)THEN
  WRITE(KLISTING,*)'NCREAD_XYZ for TRIP : Success in reading variable ',HVNAME(1:LEN_TRIM(HVNAME))
ENDIF
!
IF (LHOOK) CALL DR_HOOK('MODE_TRIP_NETCDF:NCREAD_XYZ',1,ZHOOK_HANDLE)
!
END SUBROUTINE NCREAD_XYZ
!
!-------------------------------------------------------------------------------
!
!########################################################
SUBROUTINE NCCREATE(KLISTING,HFILENAME,HTITLE,HTIMEUNIT, &
                    KNVAR,HVNAME,HVLNAME,HUNIT,PLON,PLAT,&
                    PMISSVAL,OVERBOSE,KNCID,OTIME,       &
                    KZLEN,OVARZDIM,ODOUBLE,              &
                    OLAKE,KLAKE_NUM)
!########################################################
!
!!    PURPOSE
!!    -------
!
!     Open a netcdf file name YFILENAME
!
IMPLICIT NONE
!
!*      declarations of arguments
!
CHARACTER(LEN=NF90_MAX_NAME), INTENT(IN) :: HFILENAME, HTITLE, HTIMEUNIT
!
INTEGER, INTENT(IN) :: KNVAR
CHARACTER(LEN=NF90_MAX_NAME), DIMENSION(:), INTENT(IN) :: HVNAME, HVLNAME, HUNIT
!
REAL, DIMENSION(:), INTENT(IN) :: PLON
REAL, DIMENSION(:), INTENT(IN) :: PLAT
!
LOGICAL, INTENT(IN)  :: OVERBOSE
!
REAL,    INTENT(IN)  :: PMISSVAL
!
INTEGER, INTENT(IN)  :: KLISTING
!
INTEGER, INTENT(OUT) :: KNCID
!
LOGICAL, INTENT(IN)  :: OTIME
!
INTEGER,               INTENT(IN), OPTIONAL  :: KZLEN
LOGICAL, DIMENSION(:), INTENT(IN), OPTIONAL  :: OVARZDIM
LOGICAL, DIMENSION(:), INTENT(IN), OPTIONAL  :: ODOUBLE
LOGICAL, DIMENSION(:), INTENT(IN), OPTIONAL  :: OLAKE
INTEGER,               INTENT(IN), OPTIONAL  :: KLAKE_NUM
!
!*      declarations of local variables
!
CHARACTER(LEN=NF90_MAX_NAME) :: YWORK
!
REAL*4, DIMENSION(:), ALLOCATABLE :: ZWORK
!
REAL*4,  DIMENSION(SIZE(PLON)) :: ZLON
REAL*4,  DIMENSION(SIZE(PLAT)) :: ZLAT
INTEGER, DIMENSION(:), ALLOCATABLE:: ILAKE
!
LOGICAL, DIMENSION(KNVAR) :: LDOUBLE
LOGICAL, DIMENSION(KNVAR) :: LLAKE
!
REAL*4                        :: ZMISSVAL
!
INTEGER :: ILON, ILAT
INTEGER :: ILONDIM, ILATDIM, ILEVDIM, ITIMEDIM, ILAKENUMDIM
INTEGER :: ILON_ID, ILAT_ID, ILEV_ID, ITIME_ID, VAR_ID, ILAKE_NUM_ID
INTEGER :: IC, IWORK
INTEGER :: JVAR
!
REAL(KIND=JPRB) :: ZHOOK_HANDLE
!
!*      procedure
!
IF (LHOOK) CALL DR_HOOK('MODE_TRIP_NETCDF:NCCREATE',0,ZHOOK_HANDLE)
!
IF(PRESENT(ODOUBLE))THEN
  LDOUBLE(:)=ODOUBLE(:)
ELSE
  LDOUBLE(:)=.FALSE.
ENDIF
!
IF(PRESENT(OLAKE).AND.PRESENT(KLAKE_NUM))THEN
  LLAKE(:)=OLAKE(:)
ELSE
  LLAKE(:)=.FALSE.
ENDIF
!
!Creation
!
YWORK = HFILENAME(1:LEN_TRIM(HFILENAME))
!
! IC = NF90_CREATE(YWORK,NF90_64BIT_OFFSET,KNCID)
IC = NF90_CREATE(YWORK,NF90_NETCDF4,KNCID)
IF(IC/=NF90_NOERR)THEN
  WRITE(KLISTING,*)'NCCREATE for TRIP : Error create file ',HFILENAME(1:LEN_TRIM(HFILENAME))
  WRITE(KLISTING,*)NF90_STRERROR(IC)
  CALL ABORT_TRIP('NCCREATE for TRIP : Error create file')
ELSEIF(OVERBOSE)THEN
    WRITE(KLISTING,*)'NCCREATE for TRIP : Success in creating file ',HFILENAME(1:LEN_TRIM(HFILENAME))
ENDIF
!
!Attributs
YWORK = HTITLE(1:LEN_TRIM(HTITLE))
IC = NF90_PUT_ATT(KNCID,NF90_GLOBAL,'title',YWORK)
YWORK = 'COARDS'
IC = NF90_PUT_ATT(KNCID,NF90_GLOBAL,'Conventions',YWORK)
!
!Dimensions
ILON = SIZE(PLON)
IC = NF90_DEF_DIM(KNCID,'longitude',ILON,ILONDIM)
ILAT = SIZE(PLAT)
IC = NF90_DEF_DIM(KNCID,'latitude',ILAT,ILATDIM)
IF(PRESENT(KLAKE_NUM)) IC = NF90_DEF_DIM(KNCID,'lake_id',KLAKE_NUM,ILAKENUMDIM)
IF(PRESENT(KZLEN)) IC = NF90_DEF_DIM(KNCID,'level',KZLEN,ILEVDIM)
IF(OTIME) IC = NF90_DEF_DIM(KNCID,'time',NF90_UNLIMITED,ITIMEDIM)
!
!Variable attributs
!
IC = NF90_DEF_VAR(KNCID,'longitude',NF90_FLOAT,ILONDIM,ILON_ID)
IC = NF90_DEF_VAR(KNCID,'latitude' ,NF90_FLOAT,ILATDIM,ILAT_ID)
YWORK = 'degrees_east'
IC = NF90_PUT_ATT(KNCID,ILON_ID,'units',YWORK)
YWORK = 'degrees_north'
IC = NF90_PUT_ATT(KNCID,ILAT_ID,'units',YWORK)
IF(PRESENT(KLAKE_NUM)) THEN
  IC = NF90_DEF_VAR(KNCID,'lake_id' ,NF90_INT,ILAKENUMDIM,ILAKE_NUM_ID)
  YWORK = '-'
  IC = NF90_PUT_ATT(KNCID,ILAKE_NUM_ID,'units',YWORK)
ENDIF
IF(PRESENT(KZLEN))THEN
  IC = NF90_DEF_VAR(KNCID,'level',NF90_FLOAT,ILEVDIM,ILEV_ID)
  YWORK = 'level'
  IC = NF90_PUT_ATT(KNCID,ILEV_ID,'units',YWORK)
ENDIF
!
IF(OTIME)THEN
   YWORK = HTIMEUNIT(1:LEN_TRIM(HTIMEUNIT))
   IC = NF90_DEF_VAR(KNCID,'time',NF90_INT,ITIMEDIM,ITIME_ID)
   IC = NF90_PUT_ATT(KNCID,ITIME_ID,'units',YWORK)
ENDIF
!
!Variables parametres
!
ZMISSVAL = REAL(PMISSVAL)
!
DO JVAR=1,KNVAR
  YWORK = HVNAME(JVAR)(1:LEN_TRIM(HVNAME(JVAR)))
  IF(PRESENT(KZLEN))THEN
    IF(OTIME)THEN
      IF(OVARZDIM(JVAR))THEN
        IF(LDOUBLE(JVAR))THEN
          IC = NF90_DEF_VAR(KNCID,YWORK,NF90_DOUBLE,(/ILONDIM,ILATDIM,ILEVDIM,ITIMEDIM/),VAR_ID)
        ELSE
          IC = NF90_DEF_VAR(KNCID,YWORK,NF90_FLOAT,(/ILONDIM,ILATDIM,ILEVDIM,ITIMEDIM/),VAR_ID)
        ENDIF
      ELSE
        IF(LLAKE(JVAR))THEN
          IF(LDOUBLE(JVAR))THEN
            IC = NF90_DEF_VAR(KNCID,YWORK,NF90_DOUBLE,(/ILAKENUMDIM,ITIMEDIM/),VAR_ID)
          ELSE
            IC = NF90_DEF_VAR(KNCID,YWORK,NF90_FLOAT,(/ILAKENUMDIM,ITIMEDIM/),VAR_ID)
          ENDIF
        ELSE
          IF(LDOUBLE(JVAR))THEN
            IC = NF90_DEF_VAR(KNCID,YWORK,NF90_DOUBLE,(/ILONDIM,ILATDIM,ITIMEDIM/),VAR_ID)
          ELSE
            IC = NF90_DEF_VAR(KNCID,YWORK,NF90_FLOAT,(/ILONDIM,ILATDIM,ITIMEDIM/),VAR_ID)
          ENDIF
        ENDIF
      ENDIF
    ELSE
      IF(OVARZDIM(JVAR))THEN
        IF(LDOUBLE(JVAR))THEN
          IC = NF90_DEF_VAR(KNCID,YWORK,NF90_DOUBLE,(/ILONDIM,ILATDIM,ILEVDIM/),VAR_ID)
        ELSE
          IC = NF90_DEF_VAR(KNCID,YWORK,NF90_FLOAT,(/ILONDIM,ILATDIM,ILEVDIM/),VAR_ID)
        ENDIF
      ELSE
        IF(LLAKE(JVAR))THEN
          IF(LDOUBLE(JVAR))THEN
            IC = NF90_DEF_VAR(KNCID,YWORK,NF90_DOUBLE,(/ILAKENUMDIM/),VAR_ID)
          ELSE
            IC = NF90_DEF_VAR(KNCID,YWORK,NF90_FLOAT,(/ILAKENUMDIM/),VAR_ID)
          ENDIF
        ELSE
          IF(LDOUBLE(JVAR))THEN
            IC = NF90_DEF_VAR(KNCID,YWORK,NF90_DOUBLE,(/ILONDIM,ILATDIM/),VAR_ID)
          ELSE
            IC = NF90_DEF_VAR(KNCID,YWORK,NF90_FLOAT,(/ILONDIM,ILATDIM/),VAR_ID)
          ENDIF
        ENDIF
      ENDIF
    ENDIF
  ELSE IF(OTIME)THEN
    IF(LLAKE(JVAR))THEN
      IF(LDOUBLE(JVAR))THEN
        IC = NF90_DEF_VAR(KNCID,YWORK,NF90_DOUBLE,(/ILAKENUMDIM,ITIMEDIM/),VAR_ID)
      ELSE
        IC = NF90_DEF_VAR(KNCID,YWORK,NF90_FLOAT,(/ILAKENUMDIM,ITIMEDIM/),VAR_ID)
      ENDIF
    ELSE
      IF(LDOUBLE(JVAR))THEN
        IC = NF90_DEF_VAR(KNCID,YWORK,NF90_DOUBLE,(/ILONDIM,ILATDIM,ITIMEDIM/),VAR_ID)
      ELSE
        IC = NF90_DEF_VAR(KNCID,YWORK,NF90_FLOAT,(/ILONDIM,ILATDIM,ITIMEDIM/),VAR_ID)
      ENDIF
    ENDIF
  ELSE 
    IF(LLAKE(JVAR))THEN
      IF(LDOUBLE(JVAR))THEN
        IC = NF90_DEF_VAR(KNCID,YWORK,NF90_DOUBLE,(/ILAKENUMDIM/),VAR_ID)
      ELSE
        IC = NF90_DEF_VAR(KNCID,YWORK,NF90_FLOAT,(/ILAKENUMDIM/),VAR_ID)
      ENDIF
    ELSE
      IF(LDOUBLE(JVAR))THEN
        IC = NF90_DEF_VAR(KNCID,YWORK,NF90_DOUBLE,(/ILONDIM,ILATDIM/),VAR_ID)
      ELSE
        IC = NF90_DEF_VAR(KNCID,YWORK,NF90_FLOAT,(/ILONDIM,ILATDIM/),VAR_ID)
      ENDIF
    ENDIF
  ENDIF
!
! Compression
  IC = NF90_DEF_VAR_DEFLATE(KNCID,VAR_ID,SHUFFLE=1,DEFLATE=1,DEFLATE_LEVEL=4)
!
  YWORK = HVLNAME(JVAR)(1:LEN_TRIM(HVLNAME(JVAR)))
  IC = NF90_PUT_ATT(KNCID,VAR_ID,'long_name',YWORK)
  YWORK = HUNIT(JVAR)(1:LEN_TRIM(HUNIT(JVAR)))
  IC = NF90_PUT_ATT(KNCID,VAR_ID,'units',YWORK)
  IF(LDOUBLE(JVAR))THEN
    IC = NF90_PUT_ATT(KNCID,VAR_ID,'_FillValue',PMISSVAL)
  ELSE
    IC = NF90_PUT_ATT(KNCID,VAR_ID,'_FillValue',ZMISSVAL)
  ENDIF
  !
ENDDO
!
IC = NF90_ENDDEF(KNCID)
IF(IC/=NF90_NOERR)THEN
  WRITE(KLISTING,*)'NCCREATE for TRIP : Error file definition'
  WRITE(KLISTING,*)NF90_STRERROR(IC)
  CALL ABORT_TRIP('NCCREATE for TRIP :  Error file definition')
ELSEIF(OVERBOSE)THEN
    WRITE(KLISTING,*)'NCCREATE for TRIP : file definition is ok'
ENDIF
!
!Write dimensions
!
ZLON(:)=PLON(:)
ZLAT(:)=PLAT(:)
!
IC = NF90_PUT_VAR(KNCID,ILON_ID,ZLON)
IC = NF90_PUT_VAR(KNCID,ILAT_ID,ZLAT)
!
IF(PRESENT(KZLEN))THEN
  ALLOCATE(ZWORK(KZLEN))
  DO JVAR = 1,KZLEN
     ZWORK(JVAR)=JVAR
  ENDDO
  IC = NF90_PUT_VAR(KNCID,ILEV_ID,ZWORK)
  DEALLOCATE(ZWORK)
ENDIF
!
IF(PRESENT(KLAKE_NUM))THEN
  ALLOCATE(ILAKE(KLAKE_NUM))
  DO JVAR = 1,KLAKE_NUM
     ILAKE(JVAR)=JVAR
  ENDDO
  IC = NF90_PUT_VAR(KNCID,ILAKE_NUM_ID,ILAKE)
  DEALLOCATE(ILAKE)
ENDIF
!
IF(OVERBOSE)THEN
  WRITE(KLISTING,*)'NCCREATE ',HFILENAME(1:LEN_TRIM(HFILENAME)),' for TRIP OK !'
ENDIF
!
IF (LHOOK) CALL DR_HOOK('MODE_TRIP_NETCDF:NCCREATE',1,ZHOOK_HANDLE)
!
END SUBROUTINE NCCREATE
!
!-------------------------------------------------------------------------------
!
!################################################
SUBROUTINE NCSTORE(KLISTING,KNCID,HVNAME,PWRITE, &
                   OVERBOSE,KTIMENUM,KTIMEVAL,   &
                   KLEVEL,OVARZDIM,ODOUBLE,OWRITE1D)
!################################################
!
!!    PURPOSE
!!    -------
!
!     Write in a netcdf file with illimited time if this is the case
!
IMPLICIT NONE
!
!*      declarations of arguments
!
CHARACTER(LEN=NF90_MAX_NAME), INTENT(IN) :: HVNAME
!
REAL, DIMENSION(:,:), INTENT(IN) :: PWRITE
!
INTEGER, INTENT(IN)           :: KLISTING, KNCID
!
LOGICAL, INTENT(IN)           :: OVERBOSE
!
INTEGER, INTENT(IN), OPTIONAL :: KTIMENUM
INTEGER, INTENT(IN), OPTIONAL :: KTIMEVAL

INTEGER, INTENT(IN), OPTIONAL :: KLEVEL
LOGICAL, INTENT(IN), OPTIONAL :: OVARZDIM
LOGICAL, INTENT(IN), OPTIONAL :: ODOUBLE
LOGICAL, INTENT(IN), OPTIONAL :: OWRITE1D
!
!*      declarations of local variables
!
CHARACTER(LEN=NF90_MAX_NAME) :: YWORK
!
REAL*4, DIMENSION(SIZE(PWRITE,1),SIZE(PWRITE,2)) :: ZWRITE
REAL, DIMENSION(SIZE(PWRITE,1))  :: PWRITE1D
!
INTEGER, DIMENSION(4) :: ISTART, ICOUNT, ICOUNT1D
!
INTEGER :: IUNLIMID, ITIMEID, ILENGHT, INDIM
INTEGER :: IC, IVAR_ID
LOGICAL :: LDOUBLE, LLAKE,LWRITE1D
!
REAL(KIND=JPRB) :: ZHOOK_HANDLE
!
!*      procedure
!
IF (LHOOK) CALL DR_HOOK('MODE_TRIP_NETCDF:NCSTORE',0,ZHOOK_HANDLE)
!
IF(PRESENT(ODOUBLE))THEN
  LDOUBLE=ODOUBLE
ELSE
  LDOUBLE=.FALSE.
ENDIF
!
IF(PRESENT(OWRITE1D))THEN
  LWRITE1D=OWRITE1D
ELSE
  LWRITE1D=.FALSE.
ENDIF

!
IF(PRESENT(KLEVEL).AND..NOT.PRESENT(OVARZDIM))THEN
  WRITE(KLISTING,*)'NCSTORE for TRIP : Error writing variable dimension ',HVNAME(1:LEN_TRIM(HVNAME))
  WRITE(KLISTING,*)'ILEVEL present but not LVARZDIM'
  WRITE(KLISTING,*)NF90_STRERROR(IC)
  CALL ABORT_TRIP('NCSTORE for TRIP : Error writing variable dimension')
ENDIF
!
IF(PRESENT(KTIMENUM).AND.PRESENT(KTIMEVAL))THEN
  IC = NF90_INQUIRE(KNCID,UNLIMITEDDIMID=IUNLIMID)
  IF(IUNLIMID/=-1)THEN
    IC = NF90_INQUIRE_DIMENSION(KNCID,IUNLIMID,LEN=ILENGHT)
    IF(KTIMENUM/=ILENGHT)THEN
      IC = NF90_INQ_VARID(KNCID,'time',ITIMEID)
      IC = NF90_PUT_VAR(KNCID,ITIMEID,KTIMEVAL,START=(/KTIMENUM/))
      IF(OVERBOSE)THEN
        WRITE(KLISTING,*)'NCSTORE : re-writing of time variable number=',&
                          KTIMENUM,' and value=',KTIMEVAL
      ENDIF
    ENDIF
  ENDIF
ENDIF
!
YWORK = HVNAME(1:LEN_TRIM(HVNAME))
!
IC = NF90_INQ_VARID(KNCID,YWORK,IVAR_ID)
IC = NF90_INQUIRE_VARIABLE(KNCID,IVAR_ID,NDIMS=INDIM)
!
ICOUNT(1) = SIZE(PWRITE,1)
ICOUNT(2) = SIZE(PWRITE,2)
ICOUNT(3) = 1
ICOUNT(4) = 1
!
ISTART(1) = 1
ISTART(2) = 1
!
ZWRITE(:,:) = PWRITE(:,:)
!
IF(PRESENT(OWRITE1D))THEN
  PWRITE1D(:)=ZWRITE(:,1)
ENDIF
!
IF(PRESENT(KLEVEL).AND.OVARZDIM)THEN
  ISTART(3) = KLEVEL
  IF(PRESENT(KTIMENUM).AND.PRESENT(KTIMEVAL))THEN
    ISTART(4) = KTIMENUM
    IF(LDOUBLE)THEN
      IC = NF90_PUT_VAR(KNCID,IVAR_ID,PWRITE,START=ISTART(1:4),COUNT=ICOUNT(1:4))
    ELSE
      IC = NF90_PUT_VAR(KNCID,IVAR_ID,ZWRITE,START=ISTART(1:4),COUNT=ICOUNT(1:4))
    ENDIF
  ELSE
    IF(LDOUBLE)THEN
      IC = NF90_PUT_VAR(KNCID,IVAR_ID,PWRITE,START=ISTART(1:3),COUNT=ICOUNT(1:3))
    ELSE
      IC = NF90_PUT_VAR(KNCID,IVAR_ID,ZWRITE,START=ISTART(1:3),COUNT=ICOUNT(1:3))
    ENDIF
  ENDIF
ELSEIF (.NOT.LWRITE1D) THEN
  IF(PRESENT(KTIMENUM).AND.PRESENT(KTIMEVAL))THEN
    ISTART(3) = KTIMENUM
    IF(LDOUBLE)THEN
      IC = NF90_PUT_VAR(KNCID,IVAR_ID,PWRITE,START=ISTART(1:3),COUNT=ICOUNT(1:3))
    ELSE
      IC = NF90_PUT_VAR(KNCID,IVAR_ID,ZWRITE,START=ISTART(1:3),COUNT=ICOUNT(1:3))
    ENDIF
  ELSE
    IF(LDOUBLE)THEN
      IC = NF90_PUT_VAR(KNCID,IVAR_ID,PWRITE,START=ISTART(1:2),COUNT=ICOUNT(1:2))
    ELSE
      IC = NF90_PUT_VAR(KNCID,IVAR_ID,ZWRITE,START=ISTART(1:2),COUNT=ICOUNT(1:2))
    ENDIF
  ENDIF
ELSE
  IF(PRESENT(KTIMENUM).AND.PRESENT(KTIMEVAL))THEN
    ISTART(2) = KTIMENUM
    ICOUNT(2) = 1
    IF(LDOUBLE)THEN
      IC = NF90_PUT_VAR(KNCID,IVAR_ID,PWRITE(:,1),START=ISTART(1:2),COUNT=ICOUNT(1:2))
    ELSE
      IC = NF90_PUT_VAR(KNCID,IVAR_ID,ZWRITE(:,1),START=ISTART(1:2),COUNT=ICOUNT(1:2))
    ENDIF
  ELSE
    IF(LDOUBLE)THEN
      IC = NF90_PUT_VAR(KNCID,IVAR_ID,PWRITE(:,1),START=ISTART(1:1),COUNT=ICOUNT(1:1))
    ELSE
      IC = NF90_PUT_VAR(KNCID,IVAR_ID,ZWRITE(:,1),START=ISTART(1:1),COUNT=ICOUNT(1:1))
    ENDIF
  ENDIF
ENDIF
!
IF(IC/=NF90_NOERR)THEN
  WRITE(KLISTING,*)'NCSTORE for TRIP : Error writing variable ',HVNAME(1:LEN_TRIM(HVNAME))
  WRITE(KLISTING,*)NF90_STRERROR(IC)
  CALL ABORT_TRIP('NCSTORE for TRIP : Error writing variable')
ELSEIF(OVERBOSE)THEN
  WRITE(KLISTING,*)'NCSTORE for TRIP : Success in writing variable ',HVNAME(1:LEN_TRIM(HVNAME))
  IF(PRESENT(KLEVEL))WRITE(KLISTING,*)'                   level: ',KLEVEL
ENDIF
!
IF (LHOOK) CALL DR_HOOK('MODE_TRIP_NETCDF:NCSTORE',1,ZHOOK_HANDLE)
!
END SUBROUTINE NCSTORE
!
!-------------------------------------------------------------------------------
!
!#####################################################################
SUBROUTINE NCDATE(KLISTING,KNCID,KYEAR,KMONTH,KDAY,PTIME,OVERBOSE)
!#####################################################################
!
!!    PURPOSE
!!    -------
!
!     Open a netcdf file name YFILENAME
!
IMPLICIT NONE
!
!*      declarations of arguments
!
INTEGER, INTENT(IN)          :: KLISTING
INTEGER, INTENT(IN)          :: KNCID
INTEGER, INTENT(IN)          :: KYEAR
INTEGER, INTENT(IN)          :: KMONTH
INTEGER, INTENT(IN)          :: KDAY
REAL,    INTENT(IN)          :: PTIME
!
LOGICAL, INTENT(IN)          :: OVERBOSE
!
!*      declarations of local variables
!
CHARACTER(LEN=NF90_MAX_NAME) :: YDATE, YWORK
!
REAL*4, DIMENSION(4) :: ZDATE
REAL*4               :: ZMISSVAL
!
INTEGER :: IC, IWORK, ISIZE, IDATEID
!
REAL(KIND=JPRB) :: ZHOOK_HANDLE
!
!*      procedure
!
IF (LHOOK) CALL DR_HOOK('MODE_TRIP_NETCDF:NCDATE',0,ZHOOK_HANDLE)
!
YDATE = 'date'
!
ZDATE(1) = REAL(KYEAR)
ZDATE(2) = REAL(KMONTH)
ZDATE(3) = REAL(KDAY)
ZDATE(4) = REAL(PTIME)
!
ZMISSVAL = XUNDEF
!
IC = NF90_REDEF(KNCID)
!
!Dimensions
IWORK=4
IC = NF90_DEF_DIM(KNCID,YDATE,IWORK,ISIZE)
IF(IC/=NF90_NOERR)THEN
  WRITE(KLISTING,*)'NCDATE for TRIP : Error creating date dimension '
  WRITE(KLISTING,*)NF90_STRERROR(IC)
  CALL ABORT_TRIP('NCDATE for TRIP : Error creating date dimension')
ENDIF
!
!Variable attributs
IC = NF90_DEF_VAR(KNCID,YDATE,NF90_FLOAT,ISIZE,IDATEID)
YWORK = 'current date: year month day time'
IC = NF90_PUT_ATT(KNCID,IDATEID,'long_name',YWORK)
IF(IC/=NF90_NOERR)THEN
  WRITE(KLISTING,*)'NCDATE for TRIP : Error creating date attributs'
  WRITE(KLISTING,*)NF90_STRERROR(IC)
  CALL ABORT_TRIP('NCDATE for TRIP : Error creating date attributs')
ENDIF
IC = NF90_PUT_ATT(KNCID,IDATEID,'_FillValue',ZMISSVAL)
IF(IC/=NF90_NOERR)THEN
  WRITE(KLISTING,*)'NCDATE for TRIP : Error creating date attributs'
  WRITE(KLISTING,*)NF90_STRERROR(IC)
  CALL ABORT_TRIP('NCDATE for TRIP : Error creating date attributs')
ENDIF
!
IC = NF90_ENDDEF(KNCID)
!
IC = NF90_PUT_VAR(KNCID,IDATEID,ZDATE)
!
IF(OVERBOSE)THEN
  WRITE(KLISTING,*)'NCDATE for TRIP : Sucess in writting current date'
ENDIF
!
IF (LHOOK) CALL DR_HOOK('MODE_TRIP_NETCDF:NCDATE',1,ZHOOK_HANDLE)
!
END SUBROUTINE NCDATE
!
!-------------------------------------------------------------------------------
!
END MODULE MODE_TRIP_NETCDF
