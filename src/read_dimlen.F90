!     ######
SUBROUTINE READ_DIMLEN(KLUOUT,HFILE,HVAR,KDIM,KDIMLEN)
!#############################################################
!
USE MODI_ABORT_TRIP
!
USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK
USE PARKIND1  ,ONLY : JPRB
!
USE NETCDF
!
IMPLICIT NONE
!
!*      0.1   Declarations of arguments
!
INTEGER,          INTENT(IN)       :: KLUOUT
 CHARACTER(LEN=*), INTENT(IN)       :: HFILE
 CHARACTER(LEN=*), INTENT(IN)       :: HVAR
INTEGER,          INTENT(IN)       :: KDIM
INTEGER, DIMENSION(KDIM), INTENT(OUT) :: KDIMLEN
!
!*      0.2   Declarations of local variables
!
INTEGER,DIMENSION(:),ALLOCATABLE :: IDIMIDS
!
 CHARACTER(LEN=NF90_MAX_NAME) :: YFNAME
 CHARACTER(LEN=NF90_MAX_NAME) :: YVAR
!
INTEGER :: IFILE_ID
INTEGER :: IVAR_ID
INTEGER :: INDIMS
INTEGER :: IRET
INTEGER :: JRET
INTEGER :: JDIM
!
REAL(KIND=JPRB) :: ZHOOK_HANDLE
!
!-------------------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('READ_DIMLEN',0,ZHOOK_HANDLE)
!
YFNAME = HFILE(1:LEN_TRIM(HFILE))   
YVAR   = HVAR (1:LEN_TRIM(HVAR))
!
IRET=NF90_OPEN(YFNAME,NF90_NOWRITE,IFILE_ID)
IF(IRET/=NF90_NOERR)THEN
  WRITE(KLUOUT,*)'READ_DIMLEN : error in NF90_OPEN'
  CALL ABORT_TRIP('READ_DIMLEN : error in NF90_OPEN')
ENDIF
!
IRET=NF90_INQ_VARID(IFILE_ID,YVAR,IVAR_ID)
IF(IRET/=NF90_NOERR)THEN
  WRITE(KLUOUT,*)'READ_DIMLEN : error in NF90_INQ_VARID'
  CALL ABORT_TRIP('READ_DIMLEN : error in NF90_INQ_VARID')
ENDIF
!
IRET=NF90_INQUIRE_VARIABLE(IFILE_ID,IVAR_ID,NDIMS=INDIMS)
IF(IRET/=NF90_NOERR)THEN
  WRITE(KLUOUT,*)'READ_DIMLEN : error in NF90_INQ_VARNDIMS'
  CALL ABORT_TRIP('READ_DIMLEN : error in NF90_INQ_VARNDIMS')
ENDIF
!
IF(INDIMS/=KDIM)THEN
  WRITE(KLUOUT,*)'READ_DIMLEN from TRIP_CONF90_FRC :'
  WRITE(KLUOUT,*)'Size read (',INDIMS,') /= size expected (',KDIM,')'
  CALL ABORT_TRIP('READ_DIMLEN : sizes not good')
ENDIF  
!
ALLOCATE(IDIMIDS(KDIM))
!
IRET=NF90_INQUIRE_VARIABLE(IFILE_ID,IVAR_ID,DIMIDS=IDIMIDS(1:INDIMS))
IF(IRET/=NF90_NOERR)THEN
  WRITE(KLUOUT,*)'READ_DIMLEN : error in NF90_INQ_VARDIMID'
  CALL ABORT_TRIP('READ_DIMLEN : error in NF90_INQ_VARDIMID')
ENDIF
!
DO JDIM=1,INDIMS
   IRET=NF90_INQUIRE_DIMENSION(IFILE_ID,IDIMIDS(JDIM),LEN=KDIMLEN(JDIM))
   IF(IRET/=NF90_NOERR)THEN
      WRITE(KLUOUT,*)'READ_DIMLEN : error in NF90_INQ_DIMLEN for dimension ',JDIM
      CALL ABORT_TRIP('READ_DIMLEN : error in NF90_INQ_DIMLEN')
   ENDIF
ENDDO
!
IRET=NF90_CLOSE(IFILE_ID)
IF(IRET/=NF90_NOERR)THEN
  WRITE(KLUOUT,*)'READ_DIMLEN : error in NF90_CLOSE'
  CALL ABORT_TRIP('READ_DIMLEN : error in NF90_CLOSE')
ENDIF
!
DEALLOCATE(IDIMIDS)
!
IF (LHOOK) CALL DR_HOOK('READ_DIMLEN',1,ZHOOK_HANDLE)
!-------------------------------------------------------------------------------
END SUBROUTINE READ_DIMLEN
